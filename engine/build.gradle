import org.apache.tools.ant.taskdefs.condition.Os

plugins {
    id 'java'
    id 'maven-publish'
}

group = 'uk.minersonline.minecart.engine'
version = rootProject.version

repositories {
    mavenCentral()
}


if (Os.isFamily(Os.FAMILY_UNIX)) {
    project.ext.lwjglNatives = "natives-linux"
    project.ext.imguiNatives = "natives-linux"
    def osArch = System.getProperty("os.arch")
    if (osArch.startsWith("arm") || osArch.startsWith("aarch64")) {
        project.ext.lwjglNatives += osArch.contains("64") || osArch.startsWith("armv8") ? "-arm64" : "-arm32"
    } else if  (osArch.startsWith("ppc")) {
        project.ext.lwjglNatives += "-ppc64le"
    } else if  (osArch.startsWith("riscv")) {
        project.ext.lwjglNatives += "-riscv64"
    }
} else if (Os.isFamily(Os.FAMILY_MAC)) {
    project.ext.imguiNatives = "natives-macos"
    project.ext.lwjglNatives = System.getProperty("os.arch").startsWith("aarch64") ? "natives-macos-arm64" : "natives-macos"
} else if (Os.isFamily(Os.FAMILY_WINDOWS)) {
    def osArch = System.getProperty("os.arch")
    project.ext.lwjglNatives = osArch.contains("64")
            ? "natives-windows${osArch.startsWith("aarch64") ? "-arm64" : ""}"
            : "natives-windows-x86"
    project.ext.imguiNatives = "natives-windows"
}

dependencies {
    testImplementation platform('org.junit:junit-bom:5.9.1')
    testImplementation 'org.junit.jupiter:junit-jupiter'

    implementation platform("org.lwjgl:lwjgl-bom:$lwjglVersion")

    implementation "org.lwjgl:lwjgl"
    implementation "org.lwjgl:lwjgl-assimp"
    implementation "org.lwjgl:lwjgl-bgfx"
    implementation "org.lwjgl:lwjgl-glfw"
    implementation "org.lwjgl:lwjgl-openal"
    implementation "org.lwjgl:lwjgl-opengl"
    implementation "org.lwjgl:lwjgl-par"
    implementation "org.lwjgl:lwjgl-stb"
    implementation "org.lwjgl:lwjgl-vulkan"
    runtimeOnly "org.lwjgl:lwjgl::$lwjglNatives"
    runtimeOnly "org.lwjgl:lwjgl-assimp::$lwjglNatives"
    runtimeOnly "org.lwjgl:lwjgl-bgfx::$lwjglNatives"
    runtimeOnly "org.lwjgl:lwjgl-glfw::$lwjglNatives"
    runtimeOnly "org.lwjgl:lwjgl-openal::$lwjglNatives"
    runtimeOnly "org.lwjgl:lwjgl-opengl::$lwjglNatives"
    runtimeOnly "org.lwjgl:lwjgl-par::$lwjglNatives"
    runtimeOnly "org.lwjgl:lwjgl-stb::$lwjglNatives"
    if (lwjglNatives == "natives-macos" || lwjglNatives == "natives-macos-arm64") runtimeOnly "org.lwjgl:lwjgl-vulkan::$lwjglNatives"
    implementation "org.joml:joml:${jomlVersion}"

    implementation "io.github.spair:imgui-java-binding:$imguiVersion"
    implementation "io.github.spair:imgui-java-lwjgl3:$imguiVersion"

    implementation "io.github.spair:imgui-java-$imguiNatives:$imguiVersion"
    implementation "dev.dominion.ecs:dominion-ecs-engine:0.9.0"
}

test {
    useJUnitPlatform()
}

publishing {
    repositories {
        maven {
            name = "GitHubPackages"
            url = "https://maven.pkg.github.com/Miners-Online/Minecart"
            credentials {
                username = System.getenv("GITHUB_ACTOR")
                password = System.getenv("GITHUB_TOKEN")
            }
        }
    }
}